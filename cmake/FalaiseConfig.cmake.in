# - Config file for the Falaise package
# It defines the following variables
#  Falaise_INCLUDE_DIRS - include directories for Falaise
#  Falaise_DEFINITIONS  - compile definitions needed to use Falaise
#  Falaise_LIBRARIES    - libraries to link against
#
#  Falaise_DOXYGEN_HTML_DIR - path to directory holding Doxygen docs
#                            for Falaise. Empty if docs were not built
#  Falaise_DOXYGEN_TAGFILE  - path to Doxygen tagfile for Falaise's docs
#                            Empty if docs were not built
#

#----------------------------------------------------------------------
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.
#----------------------------------------------------------------------

#-----------------------------------------------------------------------
# Locate ourselves, since all our config files should have been installed
# alongside us...
#
get_filename_component(__falaise_config_dir "${CMAKE_CURRENT_LIST_FILE}" PATH)

#-----------------------------------------------------------------------
# Use expansion variable to locate include dire from where we are
get_filename_component(Falaise_INCLUDE_DIR "${__falaise_config_dir}/@Falaise_CMAKEDIR_TO_INCLUDEDIR@" ABSOLUTE CACHE)
mark_as_advanced(Falaise_INCLUDE_DIR)

set(Falaise_INCLUDE_DIRS "${Falaise_INCLUDE_DIR}" "${Falaise_INCLUDE_DIR}/falaise")

set(Falaise_DEFINITIONS)

#-----------------------------------------------------------------------
# Handle paths/targets for third party deps here...
# Our main dependency is Bayeux, and this will supply the main third
# party deps.
# However, we may have used a builtin or system install of Bayeux,
# so abstract this to a separate file
#
include(${__falaise_config_dir}/FalaiseBayeux.cmake)

#-----------------------------------------------------------------------
# Append third party include directories to Falaise's so exposed
# headers are visible
#
list(APPEND Falaise_INCLUDE_DIRS
  ${Bayeux_INCLUDE_DIRS}
  )

#-----------------------------------------------------------------------
# Include the file listing all the imported targets.
# This is installed in the same location as us...
#
if(NOT Falaise_TARGETS_LOADED)
  include("${__falaise_config_dir}/FalaiseTargets.cmake")
  set(Falaise_TARGETS_LOADED 1)
endif()

# - Falaise itself
set(Falaise_LIBRARY Falaise CACHE FILE "Falaise library (imported)")
set(Falaise_LIBRARIES ${Falaise_LIBRARY})
mark_as_advanced(Falaise_LIBRARY)

#-----------------------------------------------------------------------
# Documentation - loaded optionally from its own file
#
include("${__falaise_config_dir}/FalaiseDocs.cmake" OPTIONAL)
